#version 420

// original https://www.shadertoy.com/view/DsKcRy

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

out vec4 glFragColor;

mat2 rot(float r){
  return mat2(cos(r),sin(r),-sin(r),cos(r));
}

float cube(vec3 p, vec3 s){
  vec3 q = abs(p);
  vec3 m = max(s-q,0.0);
  return length(max(q-s,0.0))-min(min(m.x,m.y),m.z);
}

vec2 pmod(vec2 p, float n){
  float np = 2.0*3.141592/n;
  float r = atan(p.x,p.y)-0.5*np;
  r = mod(r,np)-0.5*np;
  return length(p)*vec2(cos(r),sin(r));
}

vec4 dist(vec3 p){
  float scale = 1.0;
  p.yz *= rot(p.x*0.9-0.2*time);
  
  p.yz = pmod(p.yz,5.0);
  p.x -= 0.4*time;
  float ks = 2.8;
  p = mod(p,ks)-0.5*ks;
  
  float d1 = cube(p, vec3(scale));
  float d = d1;
  vec3 col2 = vec3(0.);
  for(int i = 0;i<5;i++){
    float d2 = cube(p,vec3(10.0*scale,scale/3.0,scale/3.0));
    float d3 = cube(p,vec3(scale/3.0,10.0*scale,scale/3.0));
    float d4 = cube(p,vec3(scale/3.0,scale/3.0,10.0*scale));
    float d234 = min(min(d2,d3),d4);
    float k = 1.5*scale/1.5; 
    p = mod(p,k)-0.5*k;
    scale /= 2.8;
    d = max(d,-d234);
    col2 += exp(-1.0*d)*vec3(0.04,0.01,0.04)*float(i);
  }
  vec3 col = vec3(0.01,0.04,0.04)*exp(-2.0*d) + 0.06*col2;
  return vec4(col,d);
}

void main(void) {
    vec2 r = resolution.xy;
    vec2 p=(gl_FragCoord.xy*2.-r)/min(r.x,r.y);

    vec3 tar = vec3(0.0,0.0,0.0);
    float radius = 0.1;
    float theta = time*0.;

    vec3 cpos = vec3(radius*cos(theta),0.0,radius*sin(theta));

    vec3 cdir = normalize(tar - cpos);
    vec3 side = cross(cdir,vec3(0,1,0));
    vec3 up = cross(side,cdir);
    float fov = 0.2;

    vec3 rd = normalize(p.x*side + p.y*up + fov * cdir);

    float d = 0.0;//distance
    float t = 0.0;//total distance
    vec3 pos = cpos;
    vec4 rsd = vec4(0.0);
    vec3 ac = vec3(0.0);

    for(int i = 0;i<50;i++){
      rsd = dist(pos);
      d = rsd.w;
      t += d;
      pos = cpos + rd*t;
      ac += rsd.xyz;
      if(d<0.000001)break;
    }

    vec3 col = ac*1.2*exp(-0.6*t);

    col = clamp(col,0.0,1.0);
    col = pow(col,vec3(1.4));

    glFragColor = vec4(col,1.0);
}
