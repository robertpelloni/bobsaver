#version 420

// Squiggles
// Dave H.

// https://www.shadertoy.com/view/4sjXRh

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

out vec4 glFragColor;

//---------------------------------------------------------------------------------------
vec2 Hash2(vec2 p) 
{
    float r = 523.0*sin(dot(p, vec2(37.3158, 53.614)));
    return vec2(fract(17.12354 * r), fract(23.15865 * r));
}

//---------------------------------------------------------------------------------------
vec2 Noise( in vec2 x )
{
    vec2 p = floor(x);
    vec2 f = fract(x);
    f = f*f*(3.0-2.0*f);
    return mix(Hash2(p), Hash2(p+1.0), f);
}

//---------------------------------------------------------------------------------------
vec2 HashMove2(vec2 p)
{
    return Noise(p*.1);
}

//---------------------------------------------------------------------------------------
float Cells(in vec2 p, in float time)
{
    vec2 f = fract(p);
    p = floor(p);
    float d = 1.0e10;
    
    for (int xo = -1; xo <= 1; xo++)
    {
        for (int yo = -1; yo <= 1; yo++)
        {
            vec2 g = vec2(xo, yo);
            vec2 tp = g + .5 + sin(time * 2.0 + 6.2831 * HashMove2(p + g)) - f;
            d = min(d, dot(tp, tp));
        }
    }
    return sqrt(d);
}

//---------------------------------------------------------------------------------------
void main(void)
{
    vec2 uv = gl_FragCoord.xy / resolution.xx;
    float ti = time;
    vec3 col = vec3(0.0);
    
    float amp = 1.0;
//    float size = 4.0 * (abs(fract(time*.01-.5)-.5)*50.0+1.0);
    float size = 77.0; 
    
    for (int i = 0; i < 32; i++)
    {
        float c = 1.0-Cells(uv * size-size*.5, ti);
        c = smoothstep(0.6+amp*.25, 1., c);
        col += amp * vec3(.8, .593, 1.95) * c;
        amp *= .88;
        ti -= .04;
    }
    gl_FragData[0] = vec4(min(col, 1.0), 1.0);
}
