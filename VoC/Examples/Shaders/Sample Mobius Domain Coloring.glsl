#version 420

// original https://www.shadertoy.com/view/wtf3DN

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

out vec4 glFragColor;

// The MIT License
// Copyright © 2019 J. M.
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions: The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software. THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

// Basic domain coloring plot (https://en.wikipedia.org/wiki/Domain_coloring)
// of a Möbius transformation

#define PI 3.14159265359
#define SCALE 5.0 // plot scale
#define SPACING 0.125 // grid line spacing

// from Fabrice Neyret, 
#define cis(a) vec2( cos(a), sin(a) )
#define cmul(A,B) ( mat2( A, -(A).y, (A).x ) * (B) )
#define cdiv(A,B) ( cmul( A, vec2( (B).x, -(B).y ) ) / dot(B,B) )

// Schlick bias function, from http://dept-info.labri.u-bordeaux.fr/~schlick/DOC/gem2.ps.gz
float bias( float a, float x )
{
    return x/((1.0/a - 2.0) * (1.0 - x) + 1.0);
}

// biased sawtooth
float my_saw( float x, float p )
{
    float xs = mod(x, 1.0);
    float xh = clamp(xs, 0.0, p);
    return 0.5 + 0.5 * bias(0.95, xh) * (1.0 - smoothstep(p, 1.0, xs));
}

// modified version of Inigo Quilez's method at https://www.shadertoy.com/view/MsS3Wc
// using "rational smoothstep" from https://tpfto.wordpress.com/2019/03/28/on-a-rational-variant-of-smoothstep/
vec3 smooth_hue( float h )
{
    vec3 rgb = clamp( abs(mod(6.0 * h + vec3(0.0, 4.0, 2.0), 6.0) - 3.0) - 1.0, 0.0, 1.0);
    return rgb * rgb * rgb/(1.0 - 3.0 * rgb * (1.0 - rgb));
}

// modified DLMF coloring, adapted from https://www.shadertoy.com/view/WtXGWN
vec3 smooth_dlmf( float h )
{
    vec3 rgb = clamp( vec3(1., -1., -1.) * abs((vec3(8., 4., 8.) * mod(h, 1.0) - vec3(4.5, 1.5, 5.5))) + vec3(-1.5, 1.5, 2.5), 0.0, 1.0 );
    return rgb * rgb * rgb/(1.0 - 3.0 * rgb * (1.0 - rgb));
}

// Möbius transformation parametrized by its fixed points g1, g2 and pole zi, https://en.wikipedia.org/wiki/M%C3%B6bius_transformation#Poles_of_the_transformation

vec2 moebius( in vec2 g1, in vec2 g2, in vec2 zi, in vec2 z )
{
    return cdiv(cmul(g1 + g2 - zi, z) - cmul(g1, g2), z - zi);
}

void main(void)
{
    vec2 z = SCALE * ((gl_FragCoord.xy/min(resolution.x, resolution.y)) - vec2(0.875, 0.5));
    
    vec2 w = moebius(0.75 * cis(-3.0 * time - PI/5.0), 1.25 * cis(2.0 * time + PI/3.0), vec2(-1.5, 0.0), z);
    float ph = atan(w.y, w.x);
    float lm = log(0.0001 + length(w));
    
    vec3 c = vec3(1.0);
    c = smooth_dlmf(0.5 * (ph / PI));
    // uncomment for HSV version 
    // c = smooth_hue(0.5 * (ph / PI));

    c *= my_saw((0.5 * (lm/PI))/SPACING, 0.95) * my_saw((0.5 * (ph / PI))/SPACING, 0.95);
    glFragColor = vec4(c, 1.0);
    
    // uncomment if you want to see white lines instead; then comment out previous two lines
    // float sat = my_saw((0.5 * (lm/PI))/SPACING, 0.95) * my_saw((0.5 * (ph / PI))/SPACING, 0.95);
    // glFragColor = vec4(mix( vec3(1.0), c, sat), 1.0);
}
