#version 420

// original https://www.shadertoy.com/view/4t3SWN

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

out vec4 glFragColor;

vec2 hash2( vec2 p )
{
    p = vec2( dot(p,vec2(63.31,127.63)), dot(p,vec2(395.467,213.799)) );
    return -1.0 + 2.0*fract(sin(p)*43141.59265);
}

void main(void)
{
    float invzoom = 100.;
    vec2 uv = invzoom*((gl_FragCoord.xy-0.5*resolution.xy)/resolution.x);
    float bounds = smoothstep(9.,10.,length(uv*vec2(0.7,0.5)));

    //cumulate information
    float a=0.;
    vec2 h = vec2(floor(7.*time), 0.);
    for(int i=0; i<50; i++){
        float s=sign(h.x);
        h = hash2(h)*vec2(15.,20.);
        a += s*atan(uv.x-h.x, uv.y-h.y);
    }
    
    //comment this out for static center
    uv += 20.*abs(hash2(h));
    
    a+=atan(uv.y, uv.x); //spirallic center more likely

    float w = 0.8; //row width
    float p=(1.-bounds)*w; //pressure
    float s = min(0.3,p); //smooth
    float l = length(uv)+0.319*a; //base rings plus information
    
    //dist -> alternate pattern
    float m = mod(l,2.);
    float v = (1.-smoothstep(2.-s,2.,m))*smoothstep(p,p+s,m);
    
    glFragColor = vec4(v,v,v,1.);
}
