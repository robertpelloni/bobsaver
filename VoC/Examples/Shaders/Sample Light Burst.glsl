#version 420

// original https://www.shadertoy.com/view/stGXzy

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

out vec4 glFragColor;

#define pi 3.14159
#define oz vec2(1,0)

// Random normalized vector
vec2 randVec(vec2 p) {
    float r = fract(sin(dot(p, vec2(12.345, 741.85)))*4563.12);
    r *= 2.0*pi;
    r += time; // Rotate gradient vector to simulate 3d noise
    return vec2(sin(r), cos(r));
}

// Seamless tiled perlin noise
float perlin(vec2 p, vec2 t) {
    vec2 f = fract(p);
    vec2 s = smoothstep(0.0, 1.0, f);
    vec2 i = floor(p);
    // Apply mod() to vertex position to make it tileable
    float a = dot(randVec(mod(i,t)), f);
    float b = dot(randVec(mod(i+oz.xy,t)), f-oz.xy);
    float c = dot(randVec(mod(i+oz.yx,t)), f-oz.yx);
    float d = dot(randVec(mod(i+oz.xx,t)), f-oz.xx);
    return mix(mix(a, b, s.x), mix(c, d, s.x), s.y);
}

// Seamless tiled fractal noise
float fbm(vec2 p, vec2 t) {
    float a = 0.5;
    float r = 0.0;
    for (int i = 0; i < 8; i++) {
        r += a*perlin(p, t);
        a *= 0.5;
        p *= 2.0;
        t *= 2.0;
    }
    return r;
}

void main(void) {
    vec2 uv = gl_FragCoord.xy/resolution.y - resolution.xy/resolution.y/2.0;
    // Modified polar coordinates
    vec2 cuv = vec2((atan(uv.x, uv.y)+pi)/(2.0*pi), 0.005/length(uv)+0.01*time);
    // Highlight at the center of the light source
    float hl = (1.0-length(uv));
    hl *= hl * hl;
    glFragColor = vec4(pow(0.9+0.5*fbm(20.0*cuv, vec2(20)), 10.0)+hl);
}
